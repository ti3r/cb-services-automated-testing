buildscript {
	ext {
		springBootVersion = '1.3.1.RELEASE'
                appName = 'cb-services-automated-testing'
	}
}

plugins {
    id 'net.researchgate.release' version '2.3.4'
    id "org.sonarqube" version '1.1'
    id "com.jfrog.artifactory" version "4.0.0"
}

apply plugin: 'groovy'
apply plugin: 'eclipse'
apply plugin: 'net.researchgate.release'
apply plugin: 'org.sonarqube'
apply plugin: 'com.jfrog.artifactory'
apply plugin: 'maven-publish'


version = "${version}"

sourceCompatibility = 1.8
targetCompatibility = 1.8

repositories {
    maven {
       url "http://jenkins.caringbridge.org:8081/artifactory/repositories"
    }
}

eclipse {
	classpath {
		 containers.remove('org.eclipse.jdt.launching.JRE_CONTAINER')
		 containers 'org.eclipse.jdt.launching.JRE_CONTAINER/org.eclipse.jdt.internal.debug.ui.launcher.StandardVMType/JavaSE-1.8'
	}
}

task wrapper(type: Wrapper) {
	gradleVersion = '2.9'
}

sourceSets {
    integrationTest {
        java {
            compileClasspath += main.output + test.output
            runtimeClasspath += main.output + test.output
            srcDir file('src/integrationTest/groovy')
        }
        //resources.srcDir file('src/integrationTest/resources')
    }
}

configurations {
    integrationTestCompile.extendsFrom testCompile
    integrationTestRuntime.extendsFrom testRuntime
}

task integrationTest(type: Test) {
    
    reports.html.enabled = true
    reports.junitXml.enabled = true
    testClassesDir = sourceSets.integrationTest.output.classesDir
    classpath = sourceSets.integrationTest.runtimeClasspath

}

dependencies {
    //CB common library
    compile(group: 'org.caringbridge', name: 'cb-services-common', version: '1.+')
    integrationTestCompile('org.codehaus.groovy:groovy-all:2.4.5')
    //Test dependencies
    integrationTestCompile('junit:junit:4.+')
    integrationTestCompile('cglib:cglib-nodep:3.2.+')
    integrationTestCompile('com.jayway.jsonpath:json-path:2.1.+')
    //testCompile('org.seleniumhq.selenium:selenium-java:2+')
    integrationTestCompile('org.spockframework:spock-core:1.0-groovy-2.4')
}


sonarqube {
    properties {
        property "sonar.host.url", "http://jenkins.caringbridge.org:9000/"
        property "sonar.projectKey", "${appName}"
        property "sonar.projectDescription", "Automated testing for the micro-services application"
        property "sonar.projectVersion", "${version}"
    }
}

publishing{
	publications {
		javaArtifact (MavenPublication) {
			from components.java
		}
	}
}

artifactory {
    contextUrl = 'http://jenkins.caringbridge.org:8081/artifactory'
    publish {
        repository {
            //publish to snapshots if name contains snapshot otherwise publish to release
            if (  version =~ /.*SNAPSHOT.*/){
                repoKey = 'libs-snapshot-local' // The Artifactory repository key to publish to
            }else{
                repoKey = 'libs-release-local'
            }
            username = "admin" // The publisher user name
            password = "password" // The publisher password
        }
        defaults {
            publications("javaArtifact")
            publishArtifacts = true
            publishPom = true
        }
    }
    resolve {
        contextUrl = 'http://jenkins.caringbridge.org:8081/artifactory'
        repository {
            repoKey = 'repositories'
        }
    }
} 